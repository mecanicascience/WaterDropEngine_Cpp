#version 450

layout (local_size_x = 256) in;


// ====== GLOBAL SCENE DATA ======
// Camera set
layout(set = 0, binding = 0) uniform SceneBuffer {
    mat4 view;
    vec4 frustum;
    vec2 zPlanes;
    int objectsCount;
    int cullingEnabled;
} inSceneData;

// Objects set
struct ObjectData {
    mat4 model;
    vec4 collisionSphere;
};
layout(std140, set = 0, binding = 1) readonly buffer ObjectBuffer {
    ObjectData objects[];
} inObjectBuffer;
// ===============================


// ======== OBJECTS DATA =========
// Objects render batches
struct ObjectBatch {
    uint batchID;
    uint indicesCount;
};
layout (set = 1, binding = 0) readonly buffer ObjectBatchesBuffer {
    ObjectBatch objects[];
} objectBatches;


// Render Batches
struct RenderBatch {
    uint firstIndex;
    uint indexCount;
    uint instanceCount;
};
layout(set = 1, binding = 1) buffer RenderBatchesBuffer {
    RenderBatch batches[];
} renderBatches;


// Objects ID (will match to gl_instanceID)
layout(set = 1, binding = 2) buffer ObjectsID {
    uint ids[];
} objectsIDs;


// Drawing commands
struct VkDrawIndexedIndirectCommand {
    uint indexCount;
    uint instanceCount;
    uint firstIndex;
    uint vertexOffset;
    uint firstInstance;
};
layout(set = 1, binding = 3) buffer CommandsBuffer {
    VkDrawIndexedIndirectCommand commands[];
} renderCommands;
// ===============================



// Return true if the object is visible on the screen
bool isVisible(uint index) {
    // Grep object data
    vec3 center = vec3(
        inObjectBuffer.objects[index].model[3][0] + inObjectBuffer.objects[index].collisionSphere.x,
        inObjectBuffer.objects[index].model[3][1] + inObjectBuffer.objects[index].collisionSphere.y,
        inObjectBuffer.objects[index].model[3][2] + inObjectBuffer.objects[index].collisionSphere.z
    );
    center = (inSceneData.view * vec4(center, 1.0f)).xyz;
    float radius = inObjectBuffer.objects[index].collisionSphere.w;
    if (radius <= 0.0)
        return true;

    // Frustrum culling
    bool visible = true;
    visible = visible && center.z * inSceneData.frustum.y - abs(center.x) * inSceneData.frustum.x > -radius;
    visible = visible && center.z * inSceneData.frustum.w - abs(center.y) * inSceneData.frustum.z > -radius;

    // The near/far plane culling uses camera space Z directly
    visible = visible && center.z + radius > inSceneData.zPlanes.x && center.z - radius < inSceneData.zPlanes.y;
    return visible;
}


void main() {
    uint gID = gl_GlobalInvocationID.x;
    if(gID < inSceneData.objectsCount) {
        // Get object batch index
        uint batchIndex = objectBatches.objects[gID].batchID;

        // If visible or culling disabled, add object to the render list
        if (inSceneData.cullingEnabled == 0 || isVisible(gID)) {
            // Add a drawing count to the gameobject batch
            uint countIndex = atomicAdd(renderBatches.batches[batchIndex].instanceCount, 1);

            // Write the object ID into the instance buffer that will map gl_instanceID
            uint instanceIndex = renderBatches.batches[batchIndex].firstIndex + countIndex;
            objectsIDs.ids[instanceIndex] = gID;

            // Create render command
            renderCommands.commands[instanceIndex].firstIndex = 0;
            renderCommands.commands[instanceIndex].vertexOffset = 0;
            renderCommands.commands[instanceIndex].indexCount = objectBatches.objects[gID].indicesCount;
            renderCommands.commands[instanceIndex].instanceCount = 1;
            renderCommands.commands[instanceIndex].firstInstance = gID;
        }
    }
}
